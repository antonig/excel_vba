Function ENCONTRARR(hay As String, needle As String, Optional start As Long = -1) As Long
   'wrapper for vba's InstrRev
   ENCONTRARR = InStrRev(hay, needle, start)
End Function

Function Re_Extract(myRange As Range, matchPattern As String, Optional i As Integer = 0, Optional j As Integer = 0) As Variant
    'devuelve el i match correspondiente a $j
    'necesita les Regex de VBS
    Dim regEx As Object
    Set regEx = CreateObject("vbscript.regexp")

    Dim m As Variant
    With regEx
        .Global = True
        .MultiLine = True
        .IgnoreCase = False
        .Pattern = matchPattern
        Set m = .Execute(myRange)
        Re_Extract = m(i).SubMatches(j)
    End With
End Function


Function entre(s As Variant, Optional f1 As Variant = Null, Optional f2 As Variant = Null, Optional f3 As Integer = 1) As Variant
   'FUNCION : devuelve parte de string, 
   '  -por posicion y longitud  (con indices negativos como slice de javascript)
   '  -entre dos separadores de 1 o mas caracteres
   '  -por una combinacion de separador, posicion o posicion, separador
   '   si el marcador de inicio es un separador, un cuarto parametro opcional permite iniciar en el n-esimo separador
   'ARGUMENTOS
   ' s string de partida 
   ' f1 separador inicial o posicion inicial (negativo desde el final), si nulo, desde el inicio
   ' f2 separador final, largo si positivo, si negativo es posicion dese el final, si nulo hasta el final
   ' f3 numero de orden del separador inicial (negativo desde el final),si nulo, primera ocueerencia
   ' =entre(s,,4) equivale a left(s,4)
   ' =entre(s,-4) equivale a right(s,4)
   
   'TODO: falta detectar argumentos no enteros
   If Not Application.IsText(s) Then entre = Null: Exit Function
   If s = vbNullString Then entre = s: Exit Function
   Dim l1, l2 As Long, i As Long, cnt As Long, t As Long
   If IsNull(f1) Then                 'no hy f1, desde principio
      l1 = 1
   ElseIf Application.IsText(f1) Then  'desde f3 esima ocurrencia de cadena f1
     If f3 >= 0 Then
      cnt = f3: t = 1: If cnt = 0 Then cnt = 1
      For i = 1 To cnt                 'si f3>=0 tantos instr  como f3
        l1 = InStr(t, s, f1)
        If l1 = 0 Then entre = vbNullString: Exit Function
        t = l1 + 1
      Next
     Else                             'si f3<0 tantos instrrev  como f3
      cnt = -f3: t = -1
      For i = 1 To cnt
        l1 = InStrRev(s, f1, t)
        If l1 = 0 Then entre = vbNullString: Exit Function
        t = l1 - 1
      Next
     End If
      l1 = l1 + Len(f1)
   Else
      If f1 > 0 Then                   'f1 es posicion desde inicio
        l1 = f1
      Else                             'f1 es posicion desde final  ( como right$ con parametro negativo)
        l1 = Len(s) + f1
        If l1 < 1 Then entre = vbNullString: Exit Function
      End If
   End If
   
   If IsNull(f2) Then                   'hasta final
      entre = Mid(s, l1): Exit Function
   ElseIf Application.IsText(f2) Then   'hasta primera ocurrencia de cadena f2 apartir de f1
      l2 = InStr(l1 + 1, s, f2)
      If l2 = 0 Then entre = Mid(s, l1): Exit Function
      l2 = l2 - l1
   Else
      If f2 > 0 Then                    'f2 es longitud salida
        l2 = f2
      Else                              'si negativo f2 es cuenta desde el final
        l2 = Len(s) + l2 - l1
      End If
      If l2 < 1 Then entre = vbNullString: Exit Function
   End If
   entre = Mid(s, l1, l2)
End Function

